apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

publish {
    userOrg = project.property("userOrg")
    repoName = project.property("repoName")
    groupId = project.property("groupId")
    artifactId = project.property("artifactId")
    publishVersion = "${project.property('version')}${project.property('versionAnnotation')}"
    desc = project.property("desc")
    website = project.property("website")
}

publishing {
    repositories {
        maven {
            url secretProperty("privateRepositoryUrl")
            name secretProperty("privateRepositoryName")
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = secretProperty("privateRepositoryUserName")
                password = secretProperty("privateRepositoryToken")
            }
        }
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'

    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

def secretProperty(String key) {
    def Properties properties = new Properties()
    properties.load(rootProject.file("secret/privateRepository.properties").newDataInputStream())
    return properties.getProperty(key, "")
}